// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.11.0'
        classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects { subproject ->
    subproject.buildscript {
        configurations.classpath {
            resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.android.tools.build'
                        && details.requested.name == 'gradle') {
                    details.useVersion '8.11.0'
                }
            }
        }
    }

    def applyJava17 = {
        subproject.afterEvaluate {
            if (subproject.hasProperty('android')) {
                subproject.android {
                    compileOptions {
                        sourceCompatibility JavaVersion.VERSION_17
                        targetCompatibility JavaVersion.VERSION_17
                    }
                }
            }
        }
    }

    subproject.plugins.withId('com.android.application') { applyJava17() }
    subproject.plugins.withId('com.android.library') { applyJava17() }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
